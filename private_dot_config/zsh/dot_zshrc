
# Source Base16 Theme
[ -f "$HOME/.config/zsh/theme.sh" ] && . ~/.config/zsh/theme.sh
# Source Pyenv
[ -d "$PYENV/.local/src/pyenv" ] && eval "$(pyenv init -)"
# opam configuration
[[ ! -r /home/quitlox/.local/share/opam/opam-init/init.zsh ]] || source /home/quitlox/.local/share/opam/opam-init/init.zsh  > /dev/null 2> /dev/null


# Colors
autoload colors && colors

############################################################
### Zsh Settings 					 ###
############################################################

setopt rmstarsilent             # Don't prompt on rm * (already aliased rm, see ./aliases.zsh)
setopt extendedglob             # Extended globbing. Allows using regular expressions with *
setopt nocaseglob               # Case insensitive globbing
setopt numericglobsort          # Sort filenames numerically when it makes sense
setopt globdots			# files beginning with . matched without explicit .

setopt rcexpandparam            # Array expension with parameters
setopt nocheckjobs          	 # Don't warn about running processes when exiting
setopt nobeep                   # No beep
setopt autocd                   # if only directory path is entered, cd there.

# History Settings
setopt extended_history 	# Record timestamp of command in HISTFILE
setopt appendhistory            # Immediately append history instead of overwriting
setopt histignorealldups        # If a new command is a duplicate, remove the older one
setopt share_history 		# Share command history data
setopt inc_append_history       # save commands are added to the history immediately,
				                # otherwise only when shell exits.

# Bash style Ctrl+Backspace (till first slash)
autoload -Uz select-word-style
select-word-style bash

############################################################
### Zsh Completion Settings 				 ###
############################################################

# Completion rleated settings
setopt always_to_end 		# Cursor moved to the end in full copmletion
setopt hash_list_all 		# Hash everything before completion
unsetopt menu_complete 		# Do not autoselect the first completion entry
setopt auto_menu 		# Show completion menu on successive tab presses

# Performance
#zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh
# Case insensitive tab completion
#zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
# [alt] first case insensitive, then complete partial words
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# Automatically find new executables in path
zstyle ':completion:*' rehash true
# Set order of different completion types
zstyle ':completion:*' completer _expand _complete _ignored _approximate
# Show file details on file completion
#zstyle ':completion:*' file-list all

############################################################
### Zinit: Init & Settings 				 ###
############################################################

HISTSIZE=999999999
export HISTFILE="$HOME/.cache/zsh/zhistory"
SAVEHIST=$HISTSIZE

# Zinit Paths
typeset -A ZINIT  # initial Zinit's hash definition
ZINIT[BIN_DIR]=$HOME/.local/src/zinit
ZINIT[HOME_DIR]=$HOME/.local/share/zinit
ZINIT[PLUGINS_DIR]=$HOME/.local/share/zinit/plugins
ZINIT[COMPLETIONS_DIR]=$HOME/.local/share/zinit/completions
ZINIT[SNIPPETS_DIR]=$HOME/.local/share/zinit/snippets
ZINIT[ZCOMPDUMP_PATH]=$HOME/.cache/zsh/zcompdump
ZPFX=~/.local/share/zinit/polaris

### Added by Zinit's installer
if [[ ! -f "$HOME/.local/src/zinit/zinit.zsh" ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})…%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/src/zinit" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

### End of Zinit's installer chunk

source ~/.local/src/zinit/zinit.zsh

autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

############################################################
### Zinit: Annexes 					 ###
############################################################

# Turbo Mode:
# Wait 0 - Instantly needed for properly navigating the shell
# Wait 1 - Often used commands, programs and extra features
# Wait 2 - Non-urgent stuff

# z-a-bin-gem-node: Provides a RVM-like solution for Ruby
# Gems, Node modules and regular binaries.
zinit load zdharma-continuum/z-a-bin-gem-node

# z-a-man: Automatically generates man pages out of plugin
# README.md files.
zinit ice has"tree"
zinit load zdharma-continuum/z-a-man


############################################################
### Zinit: Programs 				 	 ###
############################################################

### exa
# A modern replacement for 'ls'.
zinit ice wait"0a" \
  if'[[ -z "$commands[exa]" ]]' \
  from"gh-r" as"command" \
  extract mv"bin/exa* -> exa" pick"exa"
zinit load ogham/exa

### bat
# A cat clone with wings.
zinit ice wait"3" as"command" from"gh-r" \
  if'[[ -z "$commands[bat]" ]]' \
  mv"bat* -> bat" pick"bat/bat"
zinit load sharkdp/bat

### fzf
# A command-line fuzzy finder.
export FZF_COMPLETION_TRIGGER=''
function __bind_fzf_keys() {
  [[ -e /usr/share/fzf/completion.zsh ]] && source /usr/share/fzf/completion.zsh
  [[ -e /usr/share/fzf/key-bindings.zsh ]] && source /usr/share/fzf/key-bindings.zsh
  [[ -e /usr/share/fzf/completion.zsh ]] && bindkey "^T" fzf-completion
  [[ -e /usr/share/fzf/key-bindings.zsh ]] && bindkey "^I" $fzf_default_completion
}
if [[ -n "$commands[fzf]" ]]; then
  __bind_fzf_keys
fi
zinit ice wait"0b" atload'__bind_fzf_keys' \
  if'[[ -z "$commands[fzf]" ]]' \
  as"command" from"gh-r" pick"fzf"
zinit load junegunn/fzf-bin

### Git Fuzzy
zinit ice as"program" pick"bin/git-fuzzy"
zinit light bigH/git-fuzzy

### fd
zinit ice \
  wait"3" if'[[ -z "$commands[fd]" ]]' \
  as"command" from"gh-r" mv"fd* -> fd" pick"fd/fd"
zinit load sharkdp/fd

### zoxide
zinit ice wait"0" lucid \
  as"command" from"gh-r" \
  mv"zoxide*/zoxide -> zoxide" \
  atclone"./zoxide init zsh > init.zsh" \
  atpull"%atclone" src"init.zsh" nocompile'!'
zinit load ajeetdsouza/zoxide

############################################################
### Zinit: Completions 					 ###
############################################################

### Docker
# Add Support for stacking flags (e.g. docker run -it)
# By default, zsh does not parse these
zstyle ':completion:*:*:docker:*' option-stacking yes
zstyle ':completion:*:*:docker-*:*' option-stacking yes
zinit ice as"completion" blockf
zinit snippet OMZP::docker/_docker

### Cargo
zinit ice as"completion" blockf
zinit snippet OMZP::cargo/cargo.plugin.zsh

### GIT
zinit load davidde/git

############################################################
### Zinit: Scripts 					 ###
############################################################

### SSH-Agent ###
zinit ice wait"1" silent
zinit load bobsoppe/zsh-ssh-agent

### Node Version Manager (NVM)
export LESSCHARSET=utf-8
export NVM_LAZY_LOAD=true
export NVM_LAZY_LOAD_EXTRA_COMMANDS=("vim" "chezmoi")
export NVM_SYMLINK_CURRENT=true
export NVM_COMPLETION=false
export NVM_DIR="$HOME/.local/share/nvm"
zinit ice blockf wait"1"
zinit load lukechilds/zsh-nvm

### Gitignore plugin – commands gi and gii
zinit ice wait"2" \
  trigger-load'!gi;!gii'
zinit load voronkovich/gitignore.plugin.zsh

### zhooks
zinit ice wait"2" \
  trigger-load'!zhooks'
zinit load agkozak/zhooks

############################################################
### Zinit: Plugins 					 ###
############################################################

ZSH_TMUX_CONFIG="$HOME/.config/tmux/tmux.conf"
zinit ice wait"1" if'[[ -n "$commands[tmux]" ]]'
zinit snippet OMZP::tmux/tmux.plugin.zsh

### History Substring Searching
HISTORY_SUBSTRING_SEARCH_PREFIXED=true # Match with autosuggestions
function __bind_history_keys() {
  bindkey "^P" history-substring-search-up
  bindkey "^N" history-substring-search-down
  bindkey -M vicmd "k" history-substring-search-up
  bindkey -M vicmd "j" history-substring-search-down
}
zinit ice wait atload'__bind_history_keys'
zinit load zsh-users/zsh-history-substring-search

### zconvey
# Send notifications and commands to all or selected
# Zshell instances.
zinit ice wait for \
  zdharma-continuum/zconvey \
  as"command" pick"cmds/zc-bg-notify" silent \
    zdharma-continuum/zconvey

### bgnotify
# Cross-platform background notifications for long running
# commands.

# Custom Plugins & Completions
zinit wait as"none" \
  id-as"local-plugins" nocompile \
  multisrc"${ZDOTDIR}/*.zsh" \
  atpull"zinit creinstall -q ${ZDOTDIR}/completions" \
  run-atpull \
for zdharma-continuum/null

# Starship prompt
zinit ice from"gh-r" as"program" \
  atload'!eval $(starship init zsh)'
zinit load starship/starship
# This hangs the terminal, don't do this
#zinit ice from"gh-r" as"program" \
#  atclone'./starship init zsh > init.zsh; ./starship completions zsh > _starship' \
#  atpull'%atclone' src'init.zsh'
#zinit load starship/starship

# Use Atuin or FZF for history searching
# zsh-fzf-history-search
zinit ice lucid wait'0c' if'[[ -z "$commands[atuin]" ]]'
zinit light joshskidmore/zsh-fzf-history-search
# Atuin
export ATUIN_NOBIND="true"
function __bind_atuin_keys() {
  bindkey '^r' _atuin_search_widget
}
zinit ice wait'0c' if'[[ -n "$commands[atuin]" ]]' atload'__bind_atuin_keys'
zinit load ellie/atuin


# Zsh Autosuggestions
ZSH_AUTOSUGGEST_STRATEGY=(history) # Match suggestions with history-substring
zinit ice wait atload'_zsh_autosuggest_start'
zinit load zsh-users/zsh-autosuggestions

# Zsh Completions
zinit ice wait blockf atpull'zinit creinstall -q .'
zinit load zsh-users/zsh-completions

# Fast Syntax Highlighting
zinit ice wait atinit'zpcompinit; zpcdreplay'
zinit load zdharma-continuum/fast-syntax-highlighting
